

#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПодготовитьНастройки();
	УправлениеФормой(ЭтотОбъект)
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиРеквизитовКомандФормы

&НаКлиенте
Процедура IdInstanceПриИзменении(Элемент)
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ApiTokenПриИзменении(Элемент)
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура СсылкаQRНажатие(Элемент)
	НачатьЗапускПриложения(Новый ОписаниеОповещения("ЗавершитьПоказСсылки", ЭтотОбъект)
		, Элемент.Заголовок);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСтатус(Команда)
	
	ОбновитьСтатусСервиса();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПодключение(Команда)
	ОбновитьСтатусСервиса();
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьСообщение(Команда)
	
	Если Не ЗначениеЗаполнено(НомерТелефона) Тогда
		Сообщить("Номер телефона не заполнен");
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекстСообщения) Тогда
		Сообщить("Текст сообщения не заполнен");
		Возврат;
	КонецЕсли;
	
	ОтправитьСообщениеWhatsApp(Хост, IdInstance, ApiToken, НомерТелефона, ТекстСообщения);
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьСообщениеЧерезВебхук(Команда)
	ТекстПолученноеСообщение = "";
	ПОлученноеСообщение = ПолучитьВебхук(IdInstance, ApiToken);
	
	Если ПОлученноеСообщение = Неопределено Тогда
		Сообщить("Не сообщений для получения");
	Иначе
		ТекстПолученноеСообщение = ПОлученноеСообщение;
		УдалитьВебхук(IdInstance, ApiToken, deliveryTagТекущий);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьСтатусСервиса()
	
	Если ЭтотОбъект.ПроверитьЗаполнение() Тогда
		СтатусСервиса = ПолучитьСтатусСервиса(Хост, IdInstance, ApiToken);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьНастройки()
	
	Хост = ХостПоУмолчанию();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	
	Если ЗначениеЗаполнено(Форма.IdInstance) И ЗначениеЗаполнено(Форма.ApiToken) Тогда
		Элементы.СсылкаQR.Заголовок = URLСканированиеQRКода(Форма.Хост, Форма.IdInstance, Форма.ApiToken);
	Иначе
		Элементы.СсылкаQR.Заголовок = "<Введите ID Instance и API Token чтобы получить ссылку>";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьПоказСсылки(КодВозврата, ДопПараметры) Экспорт
	// Логика не требуется
КонецПроцедуры

#КонецОбласти

#Область GreeAPI

&НаКлиентеНаСервереБезКонтекста
Функция ХостПоУмолчанию()
	Возврат "api.green-api.com";
КонецФункции

Функция ХостВВебхуков()
	Возврат "localhost:7002";
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция URLСканированиеQRКода(Хост, Инстанс, Токен)
	Возврат СтрШаблон("https://%1/waInstance%2/%3", Хост, Инстанс, Токен);
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция URLМетодаСервиса(Инстанс, Токен, Метод)
	Возврат СтрШаблон("waInstance%1/%2/%3", Инстанс, Метод, Токен);
КонецФункции

&НаКлиенте
Функция ПолучитьСтатусСервиса(Хост, Инстанс, Токен)
	
	Ответ = ОтправитьGETЗапрос(Хост, URLМетодаСервиса(Инстанс, Токен, "getStateInstance"), Истина);
	Возврат НРег(Ответ.stateInstance) = "authorized";
	
КонецФункции

&НаКлиенте
Функция ОтправитьСообщениеWhatsApp(Хост, Инстанс, Токен, Телефон, Сообщение)
	
	Структура = Новый Структура;
	Структура.Вставить("chatId", "");
	Структура.Вставить("phoneNumber", Телефон);
	Структура.Вставить("message", Сообщение);
	Запись = Новый ЗаписьJSON();
	Запись.УстановитьСтроку();
	ЗаписатьJSON(Запись, Структура);
	Тело = Запись.Закрыть();
	
	Ответ = ОтправитьPOSTЗапрос(Хост, URLМетодаСервиса(Инстанс, Токен, "sendMessage"), Тело);
	Сообщить(СтрШаблон("Сообщение отправлено успешно. idMessage=%1", Ответ.idMessage));
	
КонецФункции

&НаКлиенте
Функция ПолучитьВебхук(Инстанс, Токен)
	
	Ответ = ОтправитьGETЗапрос(ХостВВебхуков(), URLМетодаСервиса(Инстанс, Токен, "receiveWebhook"), Ложь);
	Если Ответ <> Неопределено Тогда
		deliveryTagТекущий = Ответ.deliveryTag;
		Возврат Ответ.body.messageData.textMessageData.textMessage;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция УдалитьВебхук(Инстанс, Токен, ТегСообщения)
	
	Адрес = URLМетодаСервиса(Инстанс, Токен, "deleteWebhook") + "/" + ТегСообщения;
	Ответ = ОтправитьDELETEЗапрос(ХостВВебхуков(), Адрес, Ложь);
	Возврат Ответ.result;
	
КонецФункции

#КонецОбласти

#Область HttpКлиент

&НаКлиенте
Функция ОтправитьGETЗапрос(Хост, Адрес, Защищенное)
	
	Если Защищенное Тогда
		
		Сертификат = Новый ЗащищенноеСоединениеOpenSSL(
			Новый СертификатКлиентаWindows(СпособВыбораСертификатаWindows.Выбирать),
			Новый СертификатыУдостоверяющихЦентровWindows());
		
		Соединение = Новый HTTPСоединение(Хост,,,,,, Сертификат);
		
	Иначе
		
		Соединение = Новый HTTPСоединение(Хост);
		
	КонецЕсли;
	
	ВебЗапрос = Новый HTTPЗапрос(Адрес);
	Ответ = Соединение.Получить(ВебЗапрос);
	Если Ответ.КодСостояния = 200 Тогда
		ТелоОтвета = Ответ.ПолучитьТелоКакСтроку();
		Если Не ПустаяСтрока(ТелоОтвета) Тогда
			Чтение = Новый ЧтениеJSON();
			Чтение.УстановитьСтроку(ТелоОтвета);
			ПрочитанныйОтвет = ПрочитатьJSON(Чтение);
			Чтение.Закрыть();
			Возврат ПрочитанныйОтвет;
		Иначе
			Возврат Неопределено;
		КонецЕсли;
	Иначе
		ТекстОшибки = Ответ.ПолучитьТелоКакСтроку("UTF-8");
		ВызватьИсключение СтрШаблон("Сервер вернул статус %1. 
		|%2", Ответ.КодСостояния, ТекстОшибки);
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция ОтправитьPOSTЗапрос(Хост, Адрес, Тело)
	
	Сертификат = Новый ЗащищенноеСоединениеOpenSSL(
		Новый СертификатКлиентаWindows(СпособВыбораСертификатаWindows.Выбирать),
		Новый СертификатыУдостоверяющихЦентровWindows());
	
	Соединение = Новый HTTPСоединение(Хост,,,,,, Сертификат);
	
	ВебЗапрос = Новый HTTPЗапрос(Адрес);
	ВебЗапрос.Заголовки.Вставить("Content-Type", "application/json");
	
	ВебЗапрос.УстановитьТелоИзСтроки(Тело);
	Ответ = Соединение.ОтправитьДляОбработки(ВебЗапрос);
	Если Ответ.КодСостояния = 200 Тогда
		Чтение = Новый ЧтениеJSON();
		Чтение.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
		ПрочитанныйОтвет = ПрочитатьJSON(Чтение);
		Чтение.Закрыть();
		Возврат ПрочитанныйОтвет;
	Иначе
		ТекстОшибки = Ответ.ПолучитьТелоКакСтроку("UTF-8");
		ВызватьИсключение СтрШаблон("Сервер вернул статус %1. 
		|%2", Ответ.КодСостояния, ТекстОшибки);
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция ОтправитьDELETEЗапрос(Хост, Адрес, Защищенное)
	
	Если Защищенное Тогда
		
		Сертификат = Новый ЗащищенноеСоединениеOpenSSL(
			Новый СертификатКлиентаWindows(СпособВыбораСертификатаWindows.Выбирать),
			Новый СертификатыУдостоверяющихЦентровWindows());
		
		Соединение = Новый HTTPСоединение(Хост,,,,,, Сертификат);
		
	Иначе
		
		Соединение = Новый HTTPСоединение(Хост);
		
	КонецЕсли;
	
	ВебЗапрос = Новый HTTPЗапрос(Адрес);
	Ответ = Соединение.ВызватьHTTPМетод("DELETE", ВебЗапрос);
	Если Ответ.КодСостояния = 200 Тогда
		Чтение = Новый ЧтениеJSON();
		Чтение.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
		ПрочитанныйОтвет = ПрочитатьJSON(Чтение);
		Чтение.Закрыть();
		Возврат ПрочитанныйОтвет;
	Иначе
		ТекстОшибки = Ответ.ПолучитьТелоКакСтроку("UTF-8");
		ВызватьИсключение СтрШаблон("Сервер вернул статус %1. 
		|%2", Ответ.КодСостояния, ТекстОшибки);
	КонецЕсли;
	
КонецФункции

#КонецОбласти